buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.2'
        classpath "de.mannodermaus.gradle.plugins:android-junit5:2.0.0"
        classpath "com.google.dagger:hilt-android-gradle-plugin:2.57.2"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'de.mannodermaus.android-junit'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'

android {
    compileSdk 34

    defaultConfig {
        testInstrumentationRunnerArguments runnerBuilder: 'de.mannodermaus.junit5.AndroidJUnit5Builder'
        testInstrumentationRunnerArguments
        applicationId "com.example.myapplication"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // 2) Connect JUnit 5 to the runner
    }


    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    buildFeatures {
        viewBinding true
    }
    lint {
        abortOnError false
        disable 'UnsafeExperimentalUsageError', 'UnsafeExperimentalUsageWarning'
        explainIssues true
        htmlOutput file('build/reports/lint/report.html')
        showAll true
        warningsAsErrors false
        xmlOutput file('build/reports/lint/report.xml')
    }
    namespace 'com.example.myapplication'
}

junitPlatform {
    instrumentationTests {
        version = '1.9.0'
    }
}

dependencies {
    implementation 'androidx.test:monitor:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.navigation:navigation-fragment:2.9.6'
    implementation 'androidx.navigation:navigation-ui:2.9.6'
    implementation 'androidx.test:core:1.7.0'

    // Unit tests
    testImplementation platform("org.junit:junit-bom:6.0.2")
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

    // (Required) Writing and executing Unit Tests on the JUnit Platform
    testImplementation "org.mockito:mockito-core:5.21.0"

    // Instrumentation tests
    androidTestImplementation platform("org.junit:junit-bom:6.0.2")
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api"
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:1.9.0"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:1.9.0"
    androidTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    androidTestRuntimeOnly "org.junit.platform:junit-platform-launcher"
    androidTestImplementation "androidx.test:runner:1.7.0"
    androidTestImplementation ("org.mockito:mockito-core:5.21.0") {
        exclude group: 'net.bytebuddy', module: 'byte-buddy'
        exclude group: 'net.bytebuddy', module: 'byte-buddy-agent'
    }
    // Pin ByteBuddy to a Jacoco-compatible version
    androidTestImplementation 'net.bytebuddy:byte-buddy:1.14.14'
    androidTestImplementation 'net.bytebuddy:byte-buddy-agent:1.14.14'

    // https://stackoverflow.com/a/75298544
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.3.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.3.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

jacoco {
    toolVersion = '0.8.14'
}

// Fix Jacoco transformation issue with byte-buddy
configurations.all { configuration ->
    configuration.resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'net.bytebuddy') {
            details.useVersion '1.14.14'
        }
    }
}

// Exclude byte-buddy from Jacoco transformations
afterEvaluate {
    tasks.withType(Test) {
        systemProperty "net.bytebuddy.experimental", "true"
    }
}


checkstyle {
    showViolations = true
    ignoreFailures = false
    toolVersion = "13.0.0"
}